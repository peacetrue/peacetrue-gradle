plugins {
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "1.1.0"
    // https://github.com/koral--/jacoco-gradle-testkit-plugin
    id "pl.droidsonroids.jacoco.testkit" version "1.0.12"
}

// Group id must not start with 'com.github', it should be replaced with 'io.github'
group "io.github.peacetrue.gradle"
description "java build convention plugin"

repositories {
    gradlePluginPortal()
}

configurations {
    // https://stackoverflow.com/questions/30792268/loggerfactory-is-not-a-logback-loggercontext-but-logback-is-on-the-classpath
    all*.exclude module: "logback-classic"
    all*.exclude module: "slf4j-simple"
}

dependencies {
    compileOnly gradleApi()
    compileOnly 'org.springdoc:springdoc-openapi-gradle-plugin:1.8.0'
    compileOnly 'org.openapitools:openapi-generator-gradle-plugin:6.6.0'
    compileOnly 'org.asciidoctor:asciidoctor-gradle-jvm:3.3.2'

    testImplementation gradleApi()
    testImplementation gradleTestKit()
    testImplementation 'org.springframework.boot:spring-boot-gradle-plugin:2.7.14'
    testImplementation 'org.openapitools:openapi-generator-gradle-plugin:6.6.0'
    testImplementation 'org.asciidoctor:asciidoctor-gradle-jvm:3.3.2'
    testImplementation 'org.springdoc:springdoc-openapi-gradle-plugin:1.8.0'
    testImplementation 'org.apache.commons:commons-io:1.3.2'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    vcsUrl = "https://github.com/peacetrue/peacetrue-gradle.git"
    website = "https://github.com/peacetrue/peacetrue-gradle"
    plugins {
        buildConventionPlugin {
            id = "${group}.build-convention"
            displayName = "java build convention plugin"
            description = "Prepare the test environment. Automatically invoke JacocoPlugin, TestReportAggregationPlugin, JacocoReportAggregationPlugin and useJUnitPlatform"
            implementationClass = "com.github.peacetrue.gradle.plugin.BuildConventionPlugin"
            tags.set(["peacetrue", "java", "build", "convention", "test"])
        }

/*
        testArtifactsPlugin {
            id = "${group}.test-artifacts"
            displayName = "peacetrue test artifacts plugin"
            description = "Test source code can be used as a dependent class library"
            implementationClass = "com.github.peacetrue.gradle.plugin.TestArtifactsPlugin"
        }
*/
    }
}


